// PM sine LUT oscillator decay envelopes //

#define SAMPLE_RATE 22050
#define SPEAKER_PIN 11
#define BPM         120

const unsigned int LUTsize = 256;

const uint8_t table[LUTsize] PROGMEM = {
  0x80,0x83,0x86,0x89,0x8C,0x8F,0x92,0x95,0x98,0x9B,0x9E,0xA2,
  0xA5,0xA7,0xAA,0xAD,0xB0,0xB3,0xB6,0xB9,0xBC,0xBE,0xC1,0xC4,
  0xC6,0xC9,0xCB,0xCE,0xD0,0xD3,0xD5,0xD7,0xDA,0xDC,0xDE,0xE0,
  0xE2,0xE4,0xE6,0xE8,0xEA,0xEB,0xED,0xEE,0xF0,0xF1,0xF3,0xF4,
  0xF5,0xF6,0xF8,0xF9,0xFA,0xFA,0xFB,0xFC,0xFD,0xFD,0xFE,0xFE,
  0xFE,0xFF,0xFF,0xFF,0xFF,0xFF,0xFF,0xFF,0xFE,0xFE,0xFE,0xFD,
  0xFD,0xFC,0xFB,0xFA,0xFA,0xF9,0xF8,0xF6,0xF5,0xF4,0xF3,0xF1,
  0xF0,0xEE,0xED,0xEB,0xEA,0xE8,0xE6,0xE4,0xE2,0xE0,0xDE,0xDC,
  0xDA,0xD7,0xD5,0xD3,0xD0,0xCE,0xCB,0xC9,0xC6,0xC4,0xC1,0xBE,
  0xBC,0xB9,0xB6,0xB3,0xB0,0xAD,0xAA,0xA7,0xA5,0xA2,0x9E,0x9B,
  0x98,0x95,0x92,0x8F,0x8C,0x89,0x86,0x83,0x80,0x7D,0x7A,0x77,
  0x74,0x71,0x6E,0x6B,0x68,0x65,0x62,0x5E,0x5B,0x59,0x56,0x53,
  0x50,0x4D,0x4A,0x47,0x44,0x42,0x3F,0x3C,0x3A,0x37,0x35,0x32,
  0x30,0x2D,0x2B,0x29,0x26,0x24,0x22,0x20,0x1E,0x1C,0x1A,0x18,
  0x16,0x15,0x13,0x12,0x10,0x0F,0x0D,0x0C,0x0B,0x0A,0x08,0x07,
  0x06,0x06,0x05,0x04,0x03,0x03,0x02,0x02,0x02,0x01,0x01,0x01,
  0x01,0x01,0x01,0x01,0x02,0x02,0x02,0x03,0x03,0x04,0x05,0x06,
  0x06,0x07,0x08,0x0A,0x0B,0x0C,0x0D,0x0F,0x10,0x12,0x13,0x15,
  0x16,0x18,0x1A,0x1C,0x1E,0x20,0x22,0x24,0x26,0x29,0x2B,0x2D,
  0x30,0x32,0x35,0x37,0x3A,0x3C,0x3F,0x42,0x44,0x47,0x4A,0x4D,
  0x50,0x53,0x56,0x59,0x5B,0x5E,0x62,0x65,0x68,0x6B,0x6E,0x71,
  0x74,0x77,0x7A,0x7D
};

volatile uint8_t outputvalue = 127;
uint32_t pm_depth = 5000;

volatile uint16_t env_car = 65535;
volatile uint16_t env_mod = 65535;

const uint16_t env_decay_car = 20;
const uint16_t env_decay_mod = 30;

struct oscillator {
  uint32_t phase;
  uint32_t phase_increment;
} carrier, modulator;

uint32_t phaseinc(float freq) {
  return (uint32_t)(freq * LUTsize * (1UL << 16) / SAMPLE_RATE);
}

ISR(TIMER1_COMPA_vect) {

  if (env_car > env_decay_car) env_car -= env_decay_car;
  else env_car = 0;

  if (env_mod > env_decay_mod) env_mod -= env_decay_mod;
  else env_mod = 0;

  uint8_t mod_index = modulator.phase >> 16;
  int16_t mod_sample = (int16_t)pgm_read_byte(&table[mod_index]) - 128;
  int32_t phase_offset = (int32_t)mod_sample * (env_mod >> 8) * (pm_depth >> 8);

  uint32_t pm_phase = carrier.phase + phase_offset;
  uint8_t car_index = pm_phase >> 16;

  int16_t s = (int16_t)pgm_read_byte(&table[car_index]) - 128;

  s = (s * (env_car >> 8)) >> 8;

  outputvalue = 127 + s;
  OCR2A = outputvalue;

  carrier.phase += carrier.phase_increment;
  modulator.phase += modulator.phase_increment;

}

void setup() {

  pinMode(SPEAKER_PIN, OUTPUT);

  TCCR2A = _BV(COM2A1) | _BV(WGM21) | _BV(WGM20);
  TCCR2B = _BV(CS20);
  OCR2A = 127;

  cli();

  TCCR1A = 0;
  TCCR1B = _BV(WGM12) | _BV(CS10);
  OCR1A  = (F_CPU / SAMPLE_RATE) - 1;
  TIMSK1 = _BV(OCIE1A);

  sei();

}

void loop() {

  carrier.phase_increment   = phaseinc(random(110, 880));
  modulator.phase_increment = phaseinc(random(55, 440));

  env_car = 65535;
  env_mod = 65535;

  int tempo = 60000 / BPM;
  delay(tempo / 4);

}